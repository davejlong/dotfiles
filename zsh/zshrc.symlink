export TERM=xterm-256color

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for config_file ($ZSH/**/completion.sh) source $config_file
if test -d /usr/local/share/zsh-completions
then
  fpath=(/usr/local/share/zsh-completions $fpath)
fi

# Import any local zsh files
if [[ -f $HOME/.*.zsh.local ]];
then
  for config_file ($HOME/.*.zsh.local) source $config_file
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

unset config_files
